plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${dependencyManagementVersion}"
    id 'java'
}

repositories {
    mavenCentral()
}

group = 'cl.fp'
version = '0.0.1'
sourceCompatibility = '1.8'

dependencies {
    testImplementation project(path: ':pokedex-api', configuration: 'default')
    testImplementation 'io.cucumber:cucumber-java:' + cucumberVersion
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:' + cucumberVersion
    testImplementation 'io.cucumber:cucumber-spring:' + cucumberVersion
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock:' + wiremockVersion
    testImplementation 'io.rest-assured:rest-assured:4.4.0'

}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:' + springCloudVersion
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

bootJar {
    enabled = false;
}

jar {
    enabled = true;
}

task cucumberCli() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    '--glue', 'cl.fp.pokedex', 'src/test/resources',
                    '--tags', 'not @pending'
            ]
        }
    }
}
